100 GOSUB 9000'INIT/SETUP
200 DO 
202   FPSTIMER0=TICK(0)
204   IF FPSTIMER0>FPSTIMER1 THEN 
206     LOCATE 0,0:PRINT FRM
207     FRM=0
208     FPSTIMER1=TICK(0)+1000
209   ENDIF 
210   TIMER0=TICK(0)
215   IF TIMER0>TIMER1 THEN 
220     GOSUB 1600'START WITH COPY OF OLD SHIP
230     GOSUB 1700'GET INPUT AND CALC NEW SHIP
240     IF THRUST=1 THEN GOSUB 1900 'ACCELERATE
245     IF ABS(VX)>0 OR ABS(VY)>0 THEN 'DID WE MOVE
250       GOSUB 2000'APPLY DRAG
260       GOSUB 2100'UPDATE SHIP POSITION
262     ENDIF 
270     GOSUB 1500'WRAPAROUND CHECK
300     IF RDRAW=1 THEN 
310       GOSUB 1100'ERASE OLD SHIP
320       GOSUB 1000'DRAW NEW SHIP
330     ENDIF 
440     IF ABS(VX)<0.03 THEN VX=0 'COME TO A STOP
450     IF ABS(VY)<0.03 THEN VY=0 
460     IF HYPER>0 THEN HYPER=HYPER-1 'HYPER COOLDOWN
470     RDRAW=0:IF THRUST=1 THEN RDRAW=1 'REDRAW FLAG
480     OLDTHRUST=THRUST:THRUST=0
490     TEMP=POLD:POLD=PNEW:PNEW=TEMP'SHUFFLE ARRAY
495     TIMER1=TICK(0)+SHIPTIME
496     FRM=FRM+1
497   ENDIF 
500 LOOP 
600 END 
1000 '*****DRAW NEW SHIP*****
1010 COL=3:SX0=SHIP(PNEW,0):SY0=SHIP(PNEW+1,0)
1020 FOR L=1 TO 4
1030   LINE SX0+SHIP(PNEW,L),SY0+SHIP(PNEW+1,L),SX0+SHIP(PNEW,(L+1)),SY0+SHIP(PNEW+1,(L+1)),COL
1040 NEXT L
1050 LINE SX0+SHIP(PNEW,L),SY0+SHIP(PNEW+1,L),SX0+SHIP(PNEW,1),SY0+SHIP(PNEW+1,1),COL
1060 IF THRUST=1 THEN 
1061   TEMP=RND(3)
1062   IF TEMP<2 THEN 'FLICKER THE FLAME
1065     COL=172
1070     LINE SX0+SHIP(PNEW,3),SY0+SHIP(PNEW+1,3),SX0+SHIP(PNEW,6),SY0+SHIP(PNEW+1,6),COL
1080     LINE SX0+SHIP(PNEW,6),SY0+SHIP(PNEW+1,6),SX0+SHIP(PNEW,4),SY0+SHIP(PNEW+1,4),COL
1085   ENDIF 
1090 ENDIF 
1095 RETURN 
1100 '*****ERASE OLD SHIP*****
1110 COL=0:SX0=SHIP(POLD,0):SY0=SHIP(POLD+1,0)
1120 FOR L=1 TO 4
1130   LINE SX0+SHIP(POLD,L),SY0+SHIP(POLD+1,L),SX0+SHIP(POLD,(L+1)),SY0+SHIP(POLD+1,(L+1)),COL
1140 NEXT L
1150 LINE SX0+SHIP(POLD,L),SY0+SHIP(POLD+1,L),SX0+SHIP(POLD,1),SY0+SHIP(POLD+1,1),COL
1160 IF OLDTHRUST=1 THEN 
1170   LINE SX0+SHIP(POLD,3),SY0+SHIP(POLD+1,3),SX0+SHIP(POLD,6),SY0+SHIP(POLD+1,6),COL
1180   LINE SX0+SHIP(POLD,6),SY0+SHIP(POLD+1,6),SX0+SHIP(POLD,4),SY0+SHIP(POLD+1,4),COL
1190 ENDIF 
1195 RETURN 
1200 '*****SHIP RIGHT*****
1210 DIR=DIR+1
1220 IF DIR>=63 THEN DIR=DIR-63 'ROLLOVER
1230 COSTEMP=COS(DIR*ANG):SINTEMP=SIN(DIR*ANG)
1240 GOSUB 1350
1250 RETURN 
1300 '*****SHIP LEFT*****
1310 DIR=DIR-1
1320 IF DIR<0 THEN DIR=DIR+63 'ROLLOVER
1330 COSTEMP=COS(DIR*ANG):SINTEMP=SIN(DIR*ANG)
1340 GOSUB 1350
1345 RETURN 
1350 '*****ROTATE*****
1355 FOR P=1 TO 6'UPDATE NEW SHIP ARRRAY
1360   X=SHIP(0,P):Y=SHIP(1,P)
1370   SHIP(PNEW,P)=X*COSTEMP-Y*SINTEMP
1380   SHIP(PNEW+1,P)=Y*COSTEMP+X*SINTEMP
1390 NEXT P
1395 RETURN 
1400 '*****HYPERSPACE*****
1410 SX0=(RND(PSIZE(0)-14)+7)-0.5
1420 SY0=(RND(PSIZE(1)-14)+7)-0.5
1430 SHIP(PNEW,0)=SX0:SHIP(PNEW+1,0)=SY0
1440 RDRAW=1'SET REDRAW FLAG
1445 HYPER=300'RESET HYPER COUNTER
1450 RETURN 
1500 '*****WRAPAROUND*****
1510 SX0=SHIP(PNEW,0):SY0=SHIP(PNEW+1,0)
1520 IF SX0>XMAX THEN SHIP(PNEW,0)=XMIN:RDRAW=1 
1530 IF SX0<XMIN THEN SHIP(PNEW,0)=XMAX:RDRAW=1 
1540 IF SY0>YMAX THEN SHIP(PNEW+1,0)=YMIN:RDRAW=1 
1550 IF SY0<YMIN THEN SHIP(PNEW+1,0)=YMAX:RDRAW=1 
1560 RETURN 
1600 '*****COPY OLD SHIP TO NEW SHIP*****
1610 FOR P=0 TO 6
1620   SHIP(PNEW,P)=SHIP(POLD,P)
1630   SHIP(PNEW+1,P)=SHIP(POLD+1,P)
1640 NEXT P
1650 RETURN 
1700 '*****GET KEYBOARD INPUT AND CALC NEW SHIP*****
1710 A=PAD(0)
1720 IF A<>0 THEN 
1730   IF A AND 8 THEN RDRAW=1:THRUST=1 
1740   IF A AND 4 THEN RDRAW=1:GOSUB 1200 'ROT RIGHT
1750   IF A AND 1 THEN RDRAW=1:GOSUB 1300 'ROT LEFT
1760   IF A AND 2 THEN 'HYPERSPACE
1770     IF HYPER=0 THEN RDRAW=1:GOSUB 1400 
1780   ENDIF 
1790 ENDIF 
1795 RETURN 
1900 '*****ACCELERATE*****
1910 'CREATE A NORMALIZED VECTOR
1920 'IN THE DIRECTION OF TRAVEL
1930 YN=SIN(DIR*ANG):XN=COS(DIR*ANG)
1935 'ADD NORMALIZED VECTOR*ACCFACTOR TO SHIP VECTOR
1940 VX=VX+(XN*ACCFACTOR)
1950 VY=VY+(YN*ACCFACTOR)
1960 RETURN 
2000 '*****APPLY DRAG*****
2010 VX=VX-(VX*DRAGFACTOR)
2020 VY=VY-(VY*DRAGFACTOR)
2050 RETURN 
2100 '*****UPDATE SHIP POSITION*****
2110 SX0=SHIP(PNEW,0):SXY=SHIP(PNEW+1,0)
2120 SX0=SX0+VX:SY0=SY0+VY
2130 SHIP(PNEW,0)=SX0:SHIP(PNEW+1,0)=SY0
2140 RDRAW=1'UPDATED THE SHIP, SET THE FLAG
2150 RETURN 
9000 '*****INIT/SETUP*****
9001 CLS 
9010 COL=255:ANG=0.0981747704'ANG=5.625 DEGREES
9020 PI2=6.28318531:DRAGFACTOR=0.022:ACCFACTOR=0.35
9030 SX0=(PSIZE(0)/2)-0.5'CENTER OF SCREEN
9040 SY0=(PSIZE(1)/2)-0.5'FOR SHIP PLACMENT
9060 RDRAW=1:DIR=0'RDRAW FLAG & DIRECTION OF SHIP
9070 POLD=2:PNEW=4'POINTERS TO NEW AND OLD SHIP
9080 XMIN=7:YMIN=7'WRAPAROUND
9090 XMAX=PSIZE(0)-8:YMAX=PSIZE(1)-8'WRAPAROUND
9100 VX=0:VY=0'SHIP MOVEMENT VECTOR
9110 SHIPTIME=30'PAUSE BETWEEN FRAMES
9190 'SHIP GEOMETERY
9191 'SHIP ARRAY CONTAINS THREE COPIES
9192 'SHIP(0,#) ARE X COORDS, SHIP(1,#) Y COORDS
9193 'OF THE SHIP (WE NEVER CHANGE THEM)
9194 'SHIP(2,#), SHIP(3,#), SHIP(4,#) & SHIP(5,#)
9195 'ARE USED TO HOLD OLD AND NEW SHIP COPIES
9200 DIM SHIP(5,6)
9210 SHIP(0,0)=0:SHIP(1,0)=0
9220 SHIP(0,1)=7:SHIP(1,1)=0
9230 SHIP(0,2)=-5:SHIP(1,2)=4
9240 SHIP(0,3)=-3:SHIP(1,3)=2
9250 SHIP(0,4)=-3:SHIP(1,4)=-2
9260 SHIP(0,5)=-5:SHIP(1,5)=-4
9270 SHIP(0,6)=-7:SHIP(1,6)=0
9300 FOR I=2 TO 4 STEP 2'POPULATE POLD AND PNEW
9310   FOR P=0 TO 6'WITH SHIP DATA
9320     SHIP(I,P)=SHIP(0,P)
9330     SHIP(I+1,P)=SHIP(1,P)
9340   NEXT P
9350   SHIP(I,0)=SX0:SHIP(I+1,0)=SY0'CENTER IT
9360 NEXT I
9390 RETURN 
