5 CALL FONTTEST
10 CLS 
100 CALL INIT
112 INPUT "1 OR 2 PLAYERS? ";NUMPLAYERS
114 IF NUMPLAYERS>2 OR NUMPLAYERS<1 THEN GOTO 10 
116 CLS 
120 CALL SETUPSCREEN
130 CALL NEWBREAD
200 DO 
210   IF BREAD=0 THEN 
220     COLOR DEFFG,DEFBG:LOCATE XMAX-4,2
225     PRINT SNEK(0,4)+SNEK(1,4)
230     CALL NEWBREAD
240   ENDIF 
300   CALL TRIMSNEK(1)
305   IF NUMPLAYERS=2 THEN CALL TRIMSNEK(2)
310   CALL COLLISION(1)
320   CALL DRAWSNEK(1)
330   IF NUMPLAYERS=2 THEN CALL COLLISION(2)
340   IF NUMPLAYERS=2 THEN CALL DRAWSNEK(2)
390   TOG=TOG+1
395   IF TOG=3 THEN TOG=1 
400   WAIT LEVEL
410   BEEP INT(LEN(~SNEK0)/4),1:WAIT 30
420   BEEP 0
430   CALL GETINPUT(1)
435   IF NUMPLAYERS=2 THEN CALL GETINPUT(2)
440 LOOP WHILE SNEK(0,3) AND SNEK(1,3)
450 ENDIF 'BUG - NEEDED FOR CODE FORMATTING
890 GOTO &NEWGAME
900 COLOR DEFFG,DEFBG
910 LOCATE 2,8:PRINT "SNEK SAD. BYE.":WAIT 2000
920 FONT DEFFONT'RESTORE DEFAULT FONT
930 CLS 
940 END 
1000 PROC NEWBREAD'DRAW NEW BREAD SLICE
1010 DO 
1020   @X=RND(XMAX-1)+1
1030   @Y=RND(YMAX-5)+5
1040 LOOP UNTIL CHAR(@X,@Y)=0
1050 COLOR BREDFG,BREDBG
1060 CHAR @X,@Y,BRED
1070 BREAD=1:LEVEL=INT(LEVEL*0.98)
1080 RETURN 
2000 PROC TRIMSNEK(P)
2010 IF @P=1 THEN 
2020   @MAXLEN=(SNEK(0,4)+1)*GROWTH
2030   @L=LEN(~SNEK0)
2040   IF @L/3>@MAXLEN THEN 
2050     @D=POPF(~SNEK0):@X=POPF(~SNEK0):@Y=POPF(~SNEK0)
2060     COLOR SNEK(0,1),DEFBG:CHAR @X,@Y,0
2070     @D=~SNEK0(3):@X=~SNEK0(1):@Y=~SNEK0(2)
2080   ENDIF 
2090 ELSE 'PLAYER 2
2100   @MAXLEN=(SNEK(1,4)+1)*GROWTH
2110   @L=LEN(~SNEK1)
2120   IF @L/3>@MAXLEN THEN 
2130     @D=POPF(~SNEK1):@X=POPF(~SNEK1):@Y=POPF(~SNEK1)
2140     COLOR SNEK(1,1),DEFBG:CHAR @X,@Y,0
2150     @D=~SNEK1(3):@X=~SNEK1(1):@Y=~SNEK1(2)
2160   ENDIF 
2170 ENDIF 
2180 IF @L/3>@MAXLEN THEN 
2190   IF @D=0 THEN 
2200     IF TOG=@P THEN 
2210       CHAR @X,@Y,TailUL
2220     ELSE CHAR @X,@Y,TailUR
2230     ENDIF 
2240   ENDIF 
2250   IF @D=1 THEN 
2260     IF TOG=@P THEN 
2270       CHAR @X,@Y,TailDR
2280     ELSE CHAR @X,@Y,TailDL
2290     ENDIF 
2300   ENDIF 
2310   IF @D=2 THEN 
2320     IF TOG=@P THEN 
2330       CHAR @X,@Y,TailUR
2340     ELSE CHAR @X,@Y,TailDR
2350     ENDIF 
2360   ENDIF 
2370   IF @D=3 THEN 
2380     IF TOG=@P THEN 
2390       CHAR @X,@Y,TailUL
2400     ELSE CHAR @X,@Y,TailDL
2410     ENDIF 
2420   ENDIF 
2430 ENDIF 
2450 RETURN 
3000 PROC COLLISION(P)
3010 IF @P=1 THEN 
3020   @L=LEN(~SNEK0)
3030   @X=~SNEK0(@L-2):@Y=~SNEK0(@L-1)
3040   @C=CHAR(@X,@Y)
3050 ELSE 'PLAYER 2
3060   @L=LEN(~SNEK1)
3070   @X=~SNEK1(@L-2):@Y=~SNEK1(@L-1)
3080   @C=CHAR(@X,@Y)
3090 ENDIF 
3100 IF @C<>0 THEN 
3110   IF @C=BRED AND SNEK(@P-1,3)=1 THEN 
3120     BREAD=0:SNEK(@P-1,4)=SNEK(@P-1,4)+1
3130     SOUND 1,125,32,10
3140   ELSE IF @C=BODY THEN 
3150     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3160   ELSE IF @C=BOR THEN 
3170     SNEK(@P-1,3)=0:COLOR RED,DEFBG
3180   ELSE IF @C=EyeUD THEN 
3190     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3200   ELSE IF @C=EyeLR THEN 
3210     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3220   ELSE IF @C=TailUR THEN 
3230     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3240   ELSE IF @C=TailUL THEN 
3250     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3260   ELSE IF @C=TailDR THEN 
3270     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3280   ELSE IF @C=TailDL THEN 
3290     SNEK(@P-1,3)=0:COLOR RED,SNEK(@P-1,1)
3300   ENDIF 
3310 ENDIF 
3320 RETURN 
4000 PROC DRAWSNEK(P)
4010 IF @P=1 THEN 
4020   @L=LEN(~SNEK0)
4030   @XHEAD=~SNEK0(@L-2):@YHEAD=~SNEK0(@L-1)
4040   IF @L>5 THEN @XBODY=~SNEK0(@L-5):@YBODY=~SNEK0(@L-4)
4050 ELSE 'PLAYER 2
4060   @L=LEN(~SNEK1)
4070   @XHEAD=~SNEK1(@L-2):@YHEAD=~SNEK1(@L-1)
4080   IF @L>5 THEN @XBODY=~SNEK1(@L-5):@YBODY=~SNEK1(@L-4)
4090 ENDIF 
4100 IF SNEK(@P-1,3)=1 THEN 'DRAW NEW EYES AND NECK
4110   IF SNEK(@P-1,2)=0 OR SNEK(@P-1,2)=1 THEN 
4120     COLOR SNEK(@P-1,0),SNEK(@P-1,1)
4130     CHAR @XHEAD,@YHEAD,EyeUD
4140   ELSE 
4150     COLOR SNEK(@P-1,0),SNEK(@P-1,1)
4160     CHAR @XHEAD,@YHEAD,EyeLR
4170   ENDIF 
4180   IF @L>5 THEN 
4190     CHAR @XBODY,@YBODY,BODY
4200   ENDIF 
4210 ELSE 'DRAW RED X W/BG COLOR FROM COLLISION PROC
4220   CHAR @XHEAD,@YHEAD,RedX
4230   COLOR SNEK(@P-1,0),SNEK(@P-1,1)
4240   CHAR @XBODY,@YBODY,BODY
4250   SOUND 1,58,48,300'WAH WAH WAAHH SOUND
4260   WAIT 300
4270   SOUND 1,58,46,300
4280   WAIT 300
4290   SOUND 1,58,44,1500
4300 ENDIF 
4310 RETURN 
5000 PROC GETINPUT(P)
5010 IF @P=1 THEN 'PLAYER 1
5020   @L=LEN(~SNEK0)
5030   @X=~SNEK0(@L-2):@Y=~SNEK0(@L-1)
5040   @UP=25:@DN=27:@RT=26:@LT=24
5050 ELSE 'PLAYER 2
5060   @L=LEN(~SNEK1)
5070   @X=~SNEK1(@L-2):@Y=~SNEK1(@L-1)
5080   @UP=87:@DN=83:@RT=68:@LT=65
5090 ENDIF 
5100 IF KEY(@UP) AND SNEK(@P-1,2)<>1 THEN 'UP
5110   SNEK(@P-1,2)=0
5120 ELSE IF KEY(@DN) AND SNEK(@P-1,2)<>0 THEN 'DOWN
5130   SNEK(@P-1,2)=1
5140 ELSE IF KEY(@RT) AND SNEK(@P-1,2)<>3 THEN 'RIGHT
5150   SNEK(@P-1,2)=2
5160 ELSE IF KEY(@LT) AND SNEK(@P-1,2)<>2 THEN 'LEFT
5170   SNEK(@P-1,2)=3
5180 ENDIF 
5190 IF SNEK(@P-1,2)=0 THEN @Y=@Y-1 
5200 IF SNEK(@P-1,2)=1 THEN @Y=@Y+1 
5210 IF SNEK(@P-1,2)=2 THEN @X=@X+1 
5220 IF SNEK(@P-1,2)=3 THEN @X=@X-1 
5235 IF @P=1 THEN 'PLAYER 1
5240   APPEND ~SNEK0,SNEK(0,2)
5250   APPEND ~SNEK0,@X:APPEND ~SNEK0,@Y
5260 ELSE 'PLAYER 2
5270   APPEND ~SNEK1,SNEK(1,2)
5280   APPEND ~SNEK1,@X:APPEND ~SNEK1,@Y
5290 ENDIF 
5300 RETURN 
9000 PROC FONTTEST'DEFAULT FONT TEST
9010 @FCURR=SYS(1)
9020 FONT 0:@F0=SYS(1)
9030 FONT 1:@F1=SYS(1)
9040 FONT 2:@F2=SYS(1)
9050 FONT 3:@F3=SYS(1)
9060 IF @FCURR=@F0 THEN DEFFONT=0 
9065 IF @FCURR=@F1 THEN DEFFONT=1 
9070 IF @FCURR=@F2 THEN DEFFONT=2 
9080 IF @FCURR=@F3 THEN DEFFONT=3 
9090 FONT 2
9100 WAIT 100
9110 RETURN 
9200 PROC SETUPSCREEN'DRAW BORDERS & NAME
9210 COLOR 251,0'BORDER COLOR
9230 FOR @I=1 TO XMAX-1
9240   CHAR @I,YMAX,BOR
9250   CHAR @I,0,BOR
9260   CHAR @I,4,BOR
9270 NEXT @I
9280 FOR @I=0 TO YMAX
9290   CHAR XMAX,@I,BOR
9300   CHAR 0,@I,BOR
9310 NEXT @I
9320 COLOR BREDFG,BREDBG
9330 CHAR XMAX-6,2,BRED
9340 COLOR DEFFG,DEFBG
9350 LOCATE 2,2:PRINT "SNEK GAYM"
9360 LOCATE XMAX-4,2:PRINT SNEK(0,4)
9370 RETURN 
9400 PROC INTRO
9410 CLS:COLOR 166,0
9420 LOCATE 1,2:PRINT "My naym is Snek,":WAIT 700
9430 LOCATE 1,3:PRINT "my body long.":WAIT 1500
9440 LOCATE 1,4:PRINT "My hed is smol,":WAIT 1200
9450 LOCATE 1,5:PRINT "but jaw is stronk!":WAIT 1500
9460 LOCATE 1,7:PRINT "And wen it time,":WAIT 1200
9470 LOCATE 1,8:PRINT "to get me fed,":WAIT 1500
9480 LOCATE 1,9:PRINT "I open wide,":WAIT 1200
9490 LOCATE 1,10:PRINT "I monch the bred.":WAIT 2500
9495 RETURN 
9500 &NEWGAME
9505 COLOR DEFFG,DEFBG
9510 LOCATE 2,6:PRINT "MUCH LOSS!"
9520 LOCATE 2,7:PRINT "NEW GAYM? Y/N"
9530 IN1$=INKEY$
9540 IF IN1$="N" THEN GOTO 900 
9550 IF IN1$="Y" THEN 
9560   LOCATE 2,8:PRINT "YASS!"
9570   WAIT 1000:GOTO 10
9580 ENDIF 
9590 GOTO 9530
10000 PROC INIT
10001 XMAX=CSIZE(0)-1:YMAX=CSIZE(1)-1
10002 DIM SNEK(1,4)'Array for holding player data
10003 SNEK(0,0)=164'P1 Foreground color
10004 SNEK(0,1)=166'P1 Background color
10005 SNEK(0,2)=RND(4)'P1 Direction
10006 SNEK(0,3)=1'P1 Alive flag
10007 SNEK(0,4)=0'P1 Score
10010 SNEK(1,0)=132'P2 Foreground color
10011 SNEK(1,1)=134'P2 Background color
10012 SNEK(1,2)=RND(4)'P2 Direction
10013 SNEK(1,3)=1'P2 Alive flag
10014 SNEK(1,4)=0'P2 Score
10020 REM List for DIR,X&Y data of each body segment
10021 ~SNEK0=[SNEK(0,2),INT(XMAX/3*2),INT((YMAX/2)+2)]
10022 ~SNEK1=[SNEK(1,2),INT(XMAX/3),INT((YMAX/2)+2)]
10030 LEVEL=150'Speed of game
10031 GROWTH=3'Segments of growth per bread slice
10032 BREAD=0'Bread flag
10033 DEFFG=253:DEFBG=0'DEFAULT COLORS
10034 BREDFG=166:BREDBG=179'BREAD COLORS
10035 RED=228'COLOR FOR RED X CRASH
10036 BOR=1'CHAR FOR BORDERS
10037 EyeLR=94'CHAR FOR LEFT & RIGHT SNAKE EYE
10038 EyeUD=120'CHAR FOR UP & DOWN SNAKE EYE
10039 RedX=203'RED X CHAR FOR CRASH
10040 TailUL=212'TAIL CHAR FOR MOVING UP OR LEFT
10041 TailUR=213'TAIL CHAR FOR MOVING UP OR RIGHT
10042 TailDR=214'TAIL CHAR FOR MOVING DOWN OR RIGHT
10043 TailDL=215'TAIL CHAR FOR MOVING DOWN OR LEFT
10044 BODY=227'CHAR FOR SNAKE BODY
10045 BRED=233'CHAR FOR BREAD
10046 TOG=1'TOGGLE FOR TAILS
10090 RETURN 
