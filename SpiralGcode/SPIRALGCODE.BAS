10 CLS 'SPIRALGODE.BAS
20 FEED=2000
30 OPEN "LOGO.GCODE" FOR OUTPUT AS #1
40 PRINT #1,"G21"
50 PRINT #1,"G90"
60 PRINT #1,"G54"
70 CALL PENUP
80 RADIUS=1'INITIAL RADIUS (DISTANCE TO CENTER)
90 DIV=1'10 DOTS PER MM
100 STARTX=INT(460/2)-0.5'CENTER SCREEN ON X
110 STARTY=INT(224/2)-0.5'AND ON Y
120 LOAD PCX "BE_LOGO.PCX" TO (STARTX-98),(STARTY-100) SIZE 200,200
130 CIRCLE STARTX+2,STARTY,102,255,-1
140 PI=3.1415927
150 STARTX=STARTX+RADIUS'STARTING POINT
160 X1=STARTX:Y1=STARTY:CALL MOVE(X1,Y1)
170 PIX=POINT(X1,Y1)
180 'IF PIX<255 THEN PSET X1,Y1,227
190 IF PIX<>255 THEN CALL PENDOWN
200 CIRC=2*PI*RADIUS
210 DIST=CIRC/DIV
220 DEG=360/DIST
230 A=A+DEG*0.01745'CONVERT TO RADIANS
240 Y=SIN(A)*RADIUS
250 X=COS(A)*RADIUS
260 X1=STARTX+X:Y1=STARTY+Y
270 PIX=POINT(X1,Y1)
280 IF PIX=255 THEN PSET X1,Y1,227 
290 IF PIX>226 AND PEN=0 THEN CALL PENDOWN
300 IF PIX<227 AND PEN=1 THEN CALL PENUP
310 CALL MOVE(X1,Y1)
320 RADIUS=RADIUS+DEG/360'10 DOTS PER MM
330 L=L+DIV
340 IF RADIUS<100 GOTO 200 
350 LOCATE 0,1:PRINT "RADIUS=";RADIUS/8
360 LOCATE 0,2:PRINT "LENGTH=";L/8
370 CLOSE #1
380 END 
390 PROC MOVE(X1,Y1)
400 PRINT #1,"G1 X";(X1/2)-50;" Y-";Y1/2;" F";FEED
410 RETURN 
420 PROC PENDOWN
430 PEN=1
440 PRINT #1,"G1 Z0 F";FEED
450 RETURN 
460 PROC PENUP
470 PEN=0
480 PRINT #1,"G1 Z2 F";FEED
490 RETURN 
