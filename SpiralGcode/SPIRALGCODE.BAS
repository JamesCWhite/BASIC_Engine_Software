10 CLS 'CHIP.BAS
11 FEED=2000
12 OPEN "SPIRAL.GCODE" FOR OUTPUT AS #1
13 PRINT #1,"G21"
14 PRINT #1,"G90"
15 PRINT #1,"G54"
19 CALL PENUP
20 RADIUS=1'INITIAL RADIUS (DISTANCE TO CENTER)
30 DIV=1'10 DOTS PER MM
40 STARTX=INT(460/2)-0.5'CENTER SCREEN ON X
50 STARTY=INT(224/2)-0.5'AND ON Y
60 LOAD PCX "CHIP.PCX" TO (STARTX-98),(STARTY-100) SIZE 200,200
70 CIRCLE STARTX+2,STARTY,102,255,-1
80 PI=3.1415927
90 STARTX=STARTX+RADIUS'STARTING POINT
91 X1=STARTX:Y1=STARTY:CALL MOVE(X1,Y1)
100 PIX=POINT(X1,Y1)
110 IF PIX=255 THEN PSET X1,Y1,85 
120 IF PIX=255 THEN CALL PENDOWN
130 CIRC=2*PI*RADIUS
140 DIST=CIRC/DIV
150 DEG=360/DIST
160 A=A+DEG*0.01745'CONVERT TO RADIANS
170 Y=SIN(A)*RADIUS
180 X=COS(A)*RADIUS
185 X1=STARTX+X:Y1=STARTY+Y
190 PIX=POINT(X1,Y1)
200 IF PIX=255 THEN PSET X1,Y1,227 
210 IF PIX=255 AND PEN=0 THEN CALL PENDOWN
211 IF PIX<>255 AND PEN=1 THEN CALL PENUP
215 CALL MOVE(X1,Y1)
220 RADIUS=RADIUS+DEG/360'10 DOTS PER MM
230 L=L+DIV
240 IF RADIUS<100 GOTO 130 
250 LOCATE 0,1:PRINT "RADIUS=";RADIUS/8
260 LOCATE 0,2:PRINT "LENGTH=";L/8
270 CLOSE #1
280 END 
300 PROC MOVE(X1,Y1)
310 PRINT #1,"G1 X";X1/4;" Y-";Y1/4;" F";FEED
320 RETURN 
400 PROC PENDOWN
405 PEN=1
410 PRINT #1,"G1 Z0 F";FEED
420 RETURN 
500 PROC PENUP
505 PEN=0
510 PRINT #1,"G1 Z2 F";FEED
520 RETURN 
